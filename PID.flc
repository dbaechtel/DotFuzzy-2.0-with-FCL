FUNCTION_BLOCK _PID

VAR_INPUT
Error : REAL;
DeltaError : REAL;
SumError : REAL;
END_VAR

VAR_OUTPUT
ManVar : REAL;
END_VAR

FUZZIFY Error 
TERM LNegative := (-20,1) (0,0);
TERM normal := (-20,0) (0,1) (20,0);
TERM close := (-3,0) (0,1) (3,0);
TERM LPositive := (0,0) (20,1);
END_FUZZIFY

FUZZIFY DeltaError 
TERM Negative := (-100,1) (0,0);
TERM Positive := (0,0) (100,1);
END_FUZZIFY

FUZZIFY SumError 
TERM LNeg := (-100,1) (0,0);
TERM LPos := (0,0) (100,1);
END_FUZZIFY

DEFUZZIFY ManVar 
TERM LNegative := -18;
TERM SNegative := -6;
TERM SPositive := 6;
TERM LPositive := 18;
METHOD : COG;
DEFAULT := NC;
RANGE := (-20..20); 
END_DEFUZZIFY

RULEBLOCK pid
AND : MIN;
ACCU : MAX;
RULE 1 : IF Error IS LNegative THEN ManVar IS LPositive;
RULE 2 : IF Error IS LPositive THEN ManVar IS LNegative;
RULE 3 : IF Error IS normal AND DeltaError IS Positive THEN ManVar IS SNegative;
RULE 4 : IF Error IS normal AND DeltaError IS Negative THEN ManVar IS SPositive;
RULE 5 : IF Error IS close AND SumError IS LPos THEN ManVar IS SNegative;
RULE 6 : IF Error IS close AND SumError IS LNeg THEN ManVar IS SPositive;
END_RULEBLOCK

END_FUNCTION_BLOCK
